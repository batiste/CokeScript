cokescript = require("../dist/cokescript")
assert = require("assert")
vm = require("vm")

def gen(source)
  return cokescript.generateModule(source).code.trim()

def exe(js, context)
  try
    return vm.runInNewContext(js, context)
  catch(e)
    throw "JS error\n#{js}"

describe("CokeScript features test suite", def()
  
  it("Lambda function", def()
    code = gen("def test() 1")
    assert.equal(code, "function test() { return 1; }")
    assert.equal(exe(code), undefined)

    code = gen("def test(a=41 + 1)\n  return a\ntest()")
    assert.equal(exe(code), 42)

    code = gen("def test() a = 1")
    assert.equal(code, "function test() { return a = 1; }")
  )

  it("If doesn't have ;", def()
    code = gen("
if 1
  1")
    assert.equal(code, "if(1) {
  1;
}")
  )

  it("Normal function", def()
    code = gen("def test()
  1")
    assert.equal(code, "function test() {\n  1;\n}")
    assert.equal(exe(code), undefined)
  )
  
  it("Function pass", def()
    code = gen("def test()
  pass")
    assert.equal(code, "function test() {\n  \n}")
  )

  it("Function pass", def()
    def test
      pass
    assert.equal(test(), undefined)
  )

  it("Lambda", def
    assert.equal((def 1 + 1)(), 2)
  )

  it("Function addition", def()
    code = gen("def test() 1 + 3\ntest()")
    assert.equal(exe(code), 4)
  )
  
  it("Function params indentation", def()
    def test(a, b, c, d, e)
      return a + b + c + d + e
    
    assert.equal(test(1,
        2, 3,
        4,
      5),
    15)
  )
  
  it("String interpolation", def()
    def test(alpha, beta)
      return "hello #{alpha} world #{beta}"
    assert.equal(test(1, 2), "hello 1 world 2")
    toto = {tata: 42}
    assert.equal("something #{toto.tata} something", "something 42 something")
    
    code = gen("\"\#{test}\"") # for coverage
    assert.equal(exe(code, {test: 42}), "42")
  )
  
  it("Return an array", def()
    def test(a=1, b, c="test")
      return [a, b, c]
    assert.deepEqual(test(undefined, 3), [1, 3, "test"])
  )
  
  it("Return an object", def()
    def test(a=1, b, c="test")
      return {a: a, b: b, c: c}
    test(undefined, 3)
    assert.deepEqual(test(undefined, 3), {a: 1, b: 3, c: "test"})
  )
  
  it("Multiline string", def()
    a = "hello
hello"
    assert.equal(a, "hello\nhello")
  )

  it("single quoted string", def()
    a = 'hello hello'
    assert.equal(a, "hello hello")
  )

  it("not expression", def()
    code = gen("not false")
    assert.equal(exe(code), true, code)
  )
  
  it("Multiline interpolated string", def()
    w = 1
    x = 2
    y = 3
    a = "hello #{w}
hello #{x}
hello #{y}"
    assert.equal(a, "hello 1\nhello 2\nhello 3")
  )
  
  it("If else elseif", def()
    code = gen("
if a == 0
  1
elseif a == 10
  20
else
  42")
    assert.equal(exe(code, {a: 0}), 1)
    assert.equal(exe(code, {a: 10}), 20)
    assert.equal(exe(code, {a: 100}), 42)
  )
  
  it("For loop", def()
    code = gen("for k, v in array
  array[k] = v * v
array")
    assert.deepEqual(exe(code, {array: [1, 2, 3]}), [1, 4, 9])
  )
  
  it("For loop accept expression", def()
    code = gen("acc = 0
for k, v in [1, 3, 4]
  acc += v
acc")
    assert.deepEqual(exe(code, {acc: 0}), 8)
  )
  
  it("Class", def()
    code = gen("
class Test(Array)
    def constructor(a=10, b)
        this.a = b

b = Test(1, 20)
b.a")
    assert.deepEqual(exe(code), 20)

    code = gen("
class Test

  def constructor()
    this.a = 1

  def other()
    1

b = Test()
b.a")
    assert.deepEqual(exe(code, {}), 1)
  )
  
  it("Invalid syntax", def()
    t = def test() gen("a=1")
    assert.throws(t, Error)
  )
  
  it("Invalid token", def()
    t = def test() gen("a = 1;")
    assert.throws(t, Error)
  )
  
  it("Comments", def()
    code = gen("# nothing\n1 # nothing")
    assert.equal(exe(code, {}), 1)
  )

  it("Comment samedent", def()
    code = gen("1\n# nothing")
    assert.equal(code, "1;\n// nothing")
  )

  it("Comments", def()
    code = gen("# nothing\n# nothing 2")
    assert.equal(code, "// nothing\n// nothing 2")
  )

  it("Empty lines", def()
    code = gen("1\n\n2")
    assert.equal(code, "1;\n\n2;")
  )
  
  it("Array syntax on several lines", def()
    code = gen("[1, 2, 3]")
    assert.deepEqual(exe(code), [1, 2, 3])
    code = gen("[\n  1,\n  2,\n  3\n]")
    assert.deepEqual(exe(code), [1, 2, 3])
  )
  
  it("DOM gen", def()
    code = gen("dom makeDom(list)
      for item in list
        <input enabled>
        <li className=\"cls\#{item}\" data-dummy=\"1\">
          =item")

  )

  it("DOM", def()
    virtualDom = {h: def h(n, p, c) {n: n, p: p, c: c}}
    dom makeDom(list)
      for item in list
        <li className="cls#{item}" data-dummy="1">
          =item
    first_li = makeDom([1, 2, 3])[0]
    assert.equal(first_li.tagName, 'LI')
    assert.deepEqual(first_li.children, [{text: '1' }])
    assert.deepEqual(first_li.properties.attributes.className, "cls1")
    assert.deepEqual(first_li.properties.attributes['data-dummy'], '1')
  )
  
  it("Function call accept function def", def()
    code = gen("
test(1, def toto()
  return 42
, 1)")
    context = {test: def(a, b, c) b()}
    assert.equal(exe(code, context), 42)
  )

  it("Object function def 1", def()
    code = gen("
a = {a: def test()
  return 42
}
a.a()")
    assert.equal(exe(code), 42)
  )

  it("Object function def 2", def()
    code = gen("
a = {
  a: def test()
    return 43
}
a.a()")
    assert.equal(exe(code), 43)
  )

  it("Object function def 3", def()
    code = gen("
a = {
    a: def test()
      return 44
    , c: 1
}
a.a()")
    assert.equal(exe(code), 44)
  )

  it("Array and whitespace", def()
    a = [
      1, 2,
      5
    ]
    assert.deepEqual(a, [1, 2, 5])
    a = [
      1
    ]
    assert.deepEqual(a, [1])
    a = [1,
      2, 3,
    4]
    assert.deepEqual(a, [1, 2, 3, 4])
    a = [1, 3,
      2
    ]
    assert.deepEqual(a, [1, 3, 2])
  )

  it("Object and whitspace", def()
    o = {
      a: 1, b: 2,
      c: 5
    }
    o = {c: 2,
      a: 1, b: 2,
    c: 5}
  )

  it("While loop", def()
    n = 5
    while n > 0
      n = n - 1
    assert.equal(n, 0)
  )

  it("For loop", def()
    array = [1, 2, 3]
    array2 = []
    for index, value in array
      array2[index] = value * value
    assert.deepEqual(array2, [1, 4, 9])
  )

  it("Try catch", def()
    code = gen("
try
  wrong()
catch(e)
  42")
    assert.equal(exe(code), 42)
  )
  
  it("Strict comparison", def()
    code = gen("23 == \"23\"")
    assert.equal(exe(code, {}), false)
    code = gen("23 == 23")
    assert.equal(exe(code, {}), true)
    code = gen("23 != \"23\"")
    assert.equal(exe(code, {}), true)
  )
  
  it("Regular expression", def()
    code = gen("\"abc\".match(/abc/)")
    assert.equal(exe(code, {})[0], "abc")
    code = gen("\"a\/bc\".match(/a\\\/bc/)")
    assert.equal(exe(code, {})[0], "a\/bc")
    code = gen("\"Abc\ndef\".match(/abc/mig)")
    assert.equal(exe(code, {})[0], "Abc")
  )
 
  it("If expression", def()
    code = gen("2 if 1 else 3")
    assert.equal(exe(code), 2)
    code = gen("2 if 0 else 3")
    assert.equal(exe(code), 3)
    code = gen("a = 2 if 0 else 3\na")
    assert.equal(exe(code), 3)
    code = gen("a = 2 if 1\na")
    assert.equal(exe(code), 2)
    code = gen("a = 2 if false\na")
    assert.equal(exe(code), undefined)
  )

  it("Var hoisting in if", def()
    code = gen("if 1
 a = 1
else
  a = 2")
    assert.equal(code, "var a;
if(1) {
  a = 1;
} else {
  a = 2;
}")
  )

  it("Var hoisting in function", def()
    code = gen("def test()
  if 1
    a = 1
  else
    a = 2")
    assert.equal(code, "function test() {
  var a;
  if(1) {
    a = 1;
  } else {
    a = 2;
  }
}")
  )

  it("New object", def()
    code = gen("new Number(42)")
    assert.equal(exe(code), 42)
    code = gen("throw new Error(42)")
  )

  it("Value unpacking", def()
    code = gen("
a, b, c = [1, 2, 3]
c")
    assert.equal(exe(code), 3)
  )

  it("Assignement unpacking", def()
    code = gen("
a, b, c = 1, 2, 3
b")
    assert.equal(exe(code), 2)
  )

  it("Assignement mapping", def()
    code = gen("
a = {b: 1, c: 2}
hello = {world: 'a string'}
{b: a, world: hello} = {b: 'worked', world: 'mapping '}
hello.world + a.b")
    assert.equal(exe(code), "mapping worked")
  )

  it("Return comma separated", def()
    code = gen("
def test
  return 1, 1 + 1, 3

a, b, c = test()
a + b + c")
    assert.equal(exe(code), 6)
  )

)
